plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    signingConfigs {
        release {
            // Keystore with signing configs
            def keystorePropertiesFile = rootProject.file("keystore.properties")

            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.mexator.camya"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 2
        versionName "1.1 - Fix no recording issue on some devices"

        // Put version to resources
        def gitHash = "git rev-parse --short HEAD".execute().text.trim()
        resValue("string", "versionName", "$versionName - $gitHash")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Load Yandex.Disk API id and password
        def secretsPropertiesFile = rootProject.file("secrets.properties")
        def secretsProperties = new Properties()
        secretsProperties.load(new FileInputStream(secretsPropertiesFile))
        buildConfigField("String","ID","\"${secretsProperties["ID"]}\"")
        buildConfigField("String","PASSWORD","\"${secretsProperties["PASSWORD"]}\"")
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def activity_version = "1.2.0-rc01"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation 'androidx.fragment:fragment-ktx:1.3.0-rc01'

    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation("io.reactivex.rxjava2:rxkotlin:2.4.0")

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.6.0"

    implementation project (":disk-restapi-sdk")
}
