plugins {
    id 'com.android.application'
    id "org.jetbrains.kotlin.android"
}

android {
    namespace = "com.mexator.camya"
    signingConfigs {
        release {
            // Keystore with signing configs
            def keystorePropertiesFile = rootProject.file("keystore.properties")

            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    compileSdk = 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        applicationId "com.mexator.camya"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 2
        versionName "1.1 - Fix no recording issue on some devices"

        // Put version to resources
        def gitHash = "git rev-parse --short HEAD".execute().text.trim()
        resValue("string", "versionName", "$versionName - $gitHash")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Load Yandex.Disk API id and password
        def secretsPropertiesFile = rootProject.file("secrets.properties")
        def secretsProperties = new Properties()
        secretsProperties.load(new FileInputStream(secretsPropertiesFile))
        buildConfigField("String","ID","\"${secretsProperties["ID"]}\"")
        buildConfigField("String","PASSWORD","\"${secretsProperties["PASSWORD"]}\"")
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation libs.androidx.activityKtx
    implementation libs.androidx.fragmentKtx

    implementation libs.androidx.recyclerview

    implementation libs.kotlin.kotlinStdlib
    implementation libs.androidx.coreKtx
    implementation libs.androidx.appcompat

    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.lifecycleExtensions

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Rx
    implementation libs.rxjava
    implementation libs.rxjava.rxandroid
    implementation libs.rxjava.rxkotlin

    // Networking
    implementation libs.retrofit
    implementation libs.retrofit.converterGson
    implementation libs.retrofit.adapterRxjava2
    implementation libs.okhttp3.loggingInterceptor

    implementation libs.yandex.diskRestapiSdk
}
